plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '3.0.1'
    id "com.google.protobuf" version "0.9.5"
}

group 'com.logandhillon'
version '1.0.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.logandhillon.fptgame'
    mainClass = 'com.logandhillon.fptgame.GameHandler'
}

tasks.register('runDev') {
    group = 'application'
    description = 'Run the game in dev mode using ./run as the LGL base path'

    // Ensure run/ exists
    def devBasePath = project.layout.projectDirectory.dir('run').asFile
    devBasePath.mkdirs()

    // If runDev is in the task graph, configure run BEFORE it executes
    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(tasks.named('run').get())) {
            tasks.named('run', JavaExec).configure {
                jvmArgs += "-DLGL_BASE_PATH=${devBasePath.absolutePath}"
            }
        }
    }

    dependsOn tasks.named('run')
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.media']
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    // log4j
    implementation('org.apache.logging.log4j:log4j-core:2.25.3')

    // protobuf
    implementation('com.google.protobuf:protobuf-java:4.28.2')
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.24.1"
    }
}

test {
    useJUnitPlatform()
}

// derive base path for game
def lglBasePath
if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
    lglBasePath = '${user.home}/Library/Application Support/logangamelib/crossing-paths'
} else if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
    lglBasePath = '${env:APPDATA}/logangamelib/crossing-paths'
} else {
    lglBasePath = '${user.home}/.config/logangamelib/crossing-paths'
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'crossing-paths'
        jvmArgs = ["-DLGL_BASE_PATH=${lglBasePath}"]
    }


    jpackage {
        vendor = 'Logan Dhillon'
        appVersion = project.version
        installerOptions = ["--license-file", file("LICENSE").absolutePath]

        installerName = {
            switch (org.gradle.internal.os.OperatingSystem.current()) {
                case { it.isMacOsX() }: return 'Crossing Paths' // put a space on OSX binaries
                default: return 'CrossingPaths'
            }
        }()

//        imageOptions = ['--icon', 'src/main/resources/icons/app.icns']  // macOS icon
//        linux = ['--icon', 'src/main/resources/icons/app.png']          // Linux icon
//        windows = ['--icon', 'src/main/resources/icons/app.ico']        // Windows icon
    }
}

jlinkZip {
    group = 'distribution'
}
